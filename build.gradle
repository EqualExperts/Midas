apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'scala'
apply plugin: 'eclipse'
apply plugin: 'idea'

defaultTasks 'clean', 'build'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

project.ext {
    //App Config
    appConfigUrl = new URL("file:///${projectDir}/AppConfig.groovy")
    appConfig = new ConfigSlurper('configuration').parse(appConfigUrl).configuration
    manifest = appConfig.product.distribution.jar.manifest.details
    project.archivesBaseName = appConfig.product.distribution.name
    previousArchiveName = appConfig.product.distribution.previousArchiveName

    //libs
    mongoDriver = 'org.mongodb:mongo-java-driver:2.11.3'
    groovy = 'org.codehaus.groovy:groovy-all:2.1.8'
    scala = 'org.scala-lang:scala-library:2.10.2'
    junit = 'junit:junit:4.8.1'
    mockito = 'org.mockito:mockito-all:1.9.5'
    specs2 = 'org.specs2:specs2_2.10:2.2.2'
    spock = 'org.spockframework:spock-core:0.7-groovy-2.0'

    //Distribution settings
    distsTemplateDir = 'distribution-template'
    distsBinDir = "$distsDir/bin"
}

repositories {
    mavenCentral()
    maven {
        url 'http://code.google.com/p/google-maven-repository'
    }
}

dependencies {
    compile(project.ext.mongoDriver, project.ext.groovy, project.ext.scala)
    testCompile(project.ext.junit, project.ext.specs2, project.ext.mockito, project.ext.spock)
}

jar {
    manifest {
        attributes.putAll(project.ext.manifest)
    }

    doLast {
        copy {
            from (configurations.compile.asPath.split(File.pathSeparator))
            into "$libsDir"
        }
    }
}


task distribute(type:Zip, dependsOn: assemble) {
    doFirst {
        ant.mkdir(dir:"$distsBinDir")
        prepareExecutables()
    }
    from project.ext.distsBinDir
    from (project.ext.distsTemplateDir) {
        include ('**/*.txt')
    }
    from ("$libsDir") { into ('libs') }
}

def prepareExecutables() {
    def allLibFiles = new File("$libsDir").list().collect { "libs/$it" }
    new File("$distsTemplateDir").list().each { filename ->
        if (filename.startsWith('midas')) {
            def contents = new File("$distsTemplateDir/$filename").text
            new FileWriter("$distsBinDir/$filename").withWriter { writer ->
                if(filename.endsWith('.sh')) {
                    writer.append contents.replace('%%JAVA_CLASS_PATH%%', allLibFiles.collect({"\$MIDAS_HOME/$it"}).join(":"))
                }
                if(filename.endsWith('.bat')) {
                    writer.append contents.replace('%%JAVA_CLASS_PATH%%', allLibFiles.collect({"%MIDAS_HOME%/$it"}).join(";"))
                }
            }
        }
    }
}

build.dependsOn = ['check', 'distribute']